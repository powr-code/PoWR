      SUBROUTINE ZONEROT (LRED,LBLUE,EMINT,TAUSUM,PJPJ,
     $          ZFINE,OPAFINE,OPALFIN,ETAFINE,ETALFIN,SFINE,RRAY,
     $          ZRAY,XCMF,OPARAY,OPALRAY,ETARAY,ETALRAY,XN,LTOT)
C***********************************************************************
C***  FORMAL INTEGRATION ACROSS THE SCATTERING ZONE
 
C***  THE FOLLOWING ROUTINE IS A VERSION OF ZONEINT WITH THE ROLE OF
C***  ZRAY AND XCMF INTERCHANGED
C***  THEREFORE DX IS USED FOR DZ, ZFINE FOR XFINE AND SO ON ,,,
C***********************************************************************
 
C***  WPI = SQRT(PI)
      DATA WPI /1.772454/
 
      DIMENSION ZFINE(LTOT),OPAFINE(LTOT),OPALFIN(LTOT)
      DIMENSION ETAFINE(LTOT),ETALFIN(LTOT),SFINE(LTOT)
      DIMENSION ZRAY(LTOT),XCMF(LTOT),OPARAY(LTOT),OPALRAY(LTOT)
      DIMENSION ETARAY(LTOT),ETALRAY(LTOT),RRAY(LTOT)
C***  THE FOLLOWING VECTORS ARE LOCAL IN ORDER TO SUPPORT AUTO-VECTORIZATION
      DIMENSION TAU(300),DTAU(300),WTAU(300)
 
C***  XN = NUMBER OF REFINED MESH POINTS ACROSS THE SCATTERING ZONE
      N=INT(XN)
C***  DX BZW DZ IST NEGATIV
      DX=(ZRAY(LBLUE)-ZRAY(LRED))/FLOAT(N-1)
      IF (DX.GE.0.) STOP ' ZONEINX'
 
      NMAX=300
      IF (N .GT. NMAX) THEN
            CALL REMARK ('DIMENSION INSUFFICIENT')
            STOP 'ERROR'
            ENDIF
 
C***  INTERPOLATE A FINE MESH BETWEEN LRED AND LBLUE WHICH IS EQUIDISTANT IN X
 
C***  OUTER LOOP: CONSIDER THE INTERVAL L-1,L
      DO 2 L=LRED+1,LBLUE
      IF (L .EQ. LRED+1) THEN
            IA=1
            ELSE
            IA=INT((ZRAY(L-1)-ZRAY(LRED))/DX)+2
            ENDIF
      IF (L .EQ. LBLUE) THEN
            IB=N
            ELSE
            IB=INT((ZRAY(L)-ZRAY(LRED))/DX)+1
            ENDIF
C***  CUBIC INTERPOLATION OF Z AS FUNCTION OF XCMF
C***  ESTIMATE OF THE COEFFICIENTS P1 ... P4 CONCERNING INTERVAL L-1,L
      CALL CUBIC (L,LTOT,XCMF,ZRAY,P1,P2,P3,P4)
 
C***  INNER LOOP WITHIN ONE ORIGINAL GRID INTERVAL L-1,L
      DO 1 I=IA,IB
      XI=FLOAT(I-1)*DX+ZRAY(LRED)
      DXA=XI-ZRAY(L-1)
      DXB=ZRAY(L)-XI
      DXA3=DXA*DXA*DXA
      DXB3=DXB*DXB*DXB
C***  CUBIC INTERPOLATION OF ZFINE
      ZFINE(I)=P1*DXA3+P2*DXA+P3*DXB3+P4*DXB
C***  THE REMAINING QUANTITIES ARE INTERPOLATED LINEARLY IN RADIUS R
      RFINE=SQRT(PJPJ+XI*XI)
      P=(RFINE-RRAY(L-1))/(RRAY(L)-RRAY(L-1))
      Q=1.-P
C***  INTERPOLATION WEIGHTS MODIFIED: LINEAR INTERPOLATION IN R**2
      Q=Q*RRAY(L-1)*RRAY(L-1)/(RFINE*RFINE)
      P=P*RRAY(L  )*RRAY(L  )/(RFINE*RFINE)
      OPAFINE(I)=Q*OPARAY (L-1)+P*OPARAY (L)
      OPALFIN(I)=Q*OPALRAY(L-1)+P*OPALRAY(L)
      ETAFINE(I)=Q*ETARAY (L-1)+P*ETARAY (L)
      ETALFIN(I)=Q*ETALRAY(L-1)+P*ETALRAY(L)
    3 CONTINUE
 
C***  ADD LINE PLUS CONTINUUM OPACITIES, CALCULATE SOURCE FUNCTION
      PHI=EXP(-ZFINE(I)*ZFINE(I))/WPI
      OPAFINE(I)=OPAFINE(I)+PHI*OPALFIN(I)
      SFINE(I)=(ETAFINE(I)+PHI*ETALFIN(I))/OPAFINE(I)
    1 CONTINUE
    2 CONTINUE
 
C***  ESTABLISH DTAU(I) = OPTICAL DEPTH INCREMENT BETWEEN I-1 AND I
      DO 4 I=2,N
    4 DTAU(I)=-0.5*(OPAFINE(I-1)+OPAFINE(I))*DX
 
C***  ESTABLISH TAU(I) = OPTICAL DEPTH SCALE
      TAU(1)=TAUSUM
      DO 5 I=2,N
    5 TAU(I)=TAU(I-1)+DTAU(I)
      TAUSUM=TAU(N)
 
C***  TAUSC: OPTICAL DEPTH OF THE SCATTERING ZONE
      TAUSC=TAU(N)-TAU(1)
C***  IF TAUSC IS LESS THAN 1.E-10: 
C***  RETURN WITHOUT ANY ADDITIONAL EMERGENT INTENSITY FROM THE SCATTERING ZONE
C***  TO PROVIDE DIVISION BY DTAU=.0 IN THE CALCULATION OF THE FOLLOWING
C***  INTEGRATION WEIGHTS
      IF (TAUSC .LT. 1.E-10) RETURN
 
C***  CALCULATE INTEGRATION WEIGHTS EXP(-TAU) DTAU
      EXPTAU=EXP(-TAU(1))
      WTAU(1)=EXPTAU+(EXP(-TAU(2))-EXPTAU)/DTAU(2)
      DO 6 I=2,N-1
      EXPTAU=EXP(-TAU(I))
C***  WB COMES FROM THE INTERVAL I-1, I
      WB=(EXP(-TAU(I-1))-EXPTAU)/DTAU(I)
C***  WA COMES FROM THE INTERVAL I, I+1
      WA=(EXP(-TAU(I+1))-EXPTAU)/DTAU(I+1)
    6 WTAU(I)=WA+WB
      EXPTAU=EXP(-TAU(N))
      WTAU(N)=-EXPTAU-(EXPTAU-EXP(-TAU(N-1)))/DTAU(N)
 
C***  INTEGRATION SUM, USING CRAY VECTOR FUNCTION SDOT (SCALAR PRODUCT)
      EMINT=EMINT+SDOT(N,SFINE,1,WTAU,1)
 
      RETURN
      END
