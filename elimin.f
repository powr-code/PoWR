      SUBROUTINE ELIMIN (XLAM,EMFLUX,FLUXIN,U,Z,
     $          A,B,C,W,BX,WX,XJC,RADIUS,P,BCORE,DBDR,
     $          OPA,ETA,THOMSON,EDDI,ND,NP,NPDIM,ENTOT,K,
     $          IWARN, ST, BELIFI, IVERS)
C***  FEAUTRIER SCHEME FOR CONTINUOUS RADIATION TRANSFER IN SPHERICAL SYMMETRY
C***  TAPE7 = MASS STORAGE FILE FOR FEAUTRIER MATRICES
C***  LAST PARAMETER -1 IN WRITMS IS VERY IMPORTANT ]
C***  OTHERWISE "MASS STORAGE LIMIT" EXCEEDED BECAUSE OLD RECORDS ARE
C***  NOT OVERWRITTEN .
C***  ATTENTION: B AND C MUST BE LOCATED SUBSEQUENTLY IN THE MEMORY ]
      DIMENSION XJC(ND),RADIUS(ND),OPA(ND),ETA(ND),THOMSON(ND)
      DIMENSION EDDI(3,ND)
      DIMENSION P(NPDIM),BX(NPDIM,NPDIM),WX(NPDIM),B(2)
      DIMENSION U(ND,NP),Z(ND,NP)
C***  To Store Feautrier Matrices ST(94*95,89)
      DIMENSION ST((NPDIM+1)*NPDIM,ND)
      LOGICAL BELIFI
      CHARACTER*4 CKEY

C***  Operating system:                    
      COMMON / COMOS / OPSYS
      CHARACTER*8 OPSYS

C***  GAUSS-ELIMINATION
      DO 1 L=1,ND
      CALL       SETUP (L,A,B,C,W,JMAX,ND,NP,NPDIM,OPA,ETA,THOMSON,
     $          XLAM,Z,RADIUS,BCORE,DBDR,XIMINUS,ENTOT,K, IVERS)
      IF (L.EQ.1) GOTO 6
      CALL MDMV (A,BX,JMAX,NPDIM)
      CALL MSUB (B,BX,JMAX,NPDIM)
      CALL MDV (A,WX,JMAX)
      CALL VADD (W,WX,JMAX)
    6 CONTINUE
      IF (XLAM .LE. 4. .AND. OPSYS(1:4) .EQ. 'CRAY') THEN
        CKEY = 'OWN'
      ELSE
        CKEY = 'FREE'
      ENDIF
      CALL INV (JMAX,NPDIM,B,CKEY)
      CALL MVV (WX,B,W,JMAX,JMAX,NPDIM)
      IF (L.EQ.ND) GOTO 2
      CALL MVMD (BX,B,C,JMAX,JMAX-1,NPDIM)
C***  COMPRESSING THE MATRIX BX  AND VECTOR WX  INTO THE RANGE OF B AND C
      DO 7 J=1,JMAX
      JC=1+(J-1)*JMAX
    7 CALL EQUAL (B (JC)  ,BX(1,J),JMAX)
      CALL EQUAL (B (JMAX*JMAX+1)  ,WX,JMAX)
      LL=JMAX*(JMAX+1)
      IF (BELIFI) THEN
        CALL WRITMS (7,B ,LL,L,-1, IDUMMY, IERR)
      ELSE
        DO I=1, LL
          ST(I,L) = B(I)
        ENDDO
      ENDIF
    1 CONTINUE
 
C***  BACK SUBSTITUTION
C***  RECENT WX IS THE FEAUTRIER-INTENSITY U AT THE INNER BOUNDARY
    2 CALL MOMENT0 (ND,RADIUS,ND,JMAX,Z,WX,XJC(ND),.FALSE.)
      CALL MOMENT1 (RADIUS(ND),JMAX,P,WX,H)
      HPLUS=BCORE/2. + DBDR/3./OPA(ND)
      CALL MOMENT2 (RADIUS(ND),JMAX,P,WX,XK)
C***  EDDI(1,L) IS THE EDDINGTON FACTOR  F = K / J
C***  EDDI(2,L) IS THE SPHERICITY FACTOR Q
C***  EDDI(3,L) IS THE EDDINGTON FACTOR H / J  (ONLY AT THE BOUNDARIES)
C***  EDDI(3,ND-1) IS THE OUTWARD FLUX HPLUS AT THE INNER BOUNDARY
      IF (XJC(ND) .GT. .0) THEN
         EDDI(1,ND)=XK/XJC(ND)
      ELSE
         EDDI(1,ND)=1./3.
         IWARN = IWARN + 1
      ENDIF

      EDDI(2,ND)=1.
      RRQ=1.
      IF (XJC(ND) .GT. .0) THEN
         EDDI(3,ND)=H/XJC(ND)
      ELSE
         EDDI(3,ND)=0.5
         IWARN = IWARN + 1
      ENDIF
 
      EDDI(3,ND-1)=HPLUS
      FLUXIN=4*(HPLUS-H)
      IF (EDDI(1,ND) .LT. 0.01) THEN
        IWARN = IWARN + 1
        EDDI(1,ND) = 0.01
      ENDIF
      FL=3.-1./EDDI(1,ND)
      DO 5 J=1,JMAX
    5 U(ND,J)=WX(J)
      CALL EQUAL (A,WX,JMAX)
      NC2=NP-ND+2
 
C***  L = ND-1 ... 1
      DO 4 JMAX=NC2,NP
      L=NP+1-JMAX
      RL=RADIUS(L)
      LL=JMAX*(JMAX+1)
      IF (BELIFI) THEN
        CALL READMS (7,B ,LL,L, IERR)
      ELSE
        DO I=1, LL
          B(I) = ST(I,L)
        ENDDO
      ENDIF
C***  DECOMPRESSING THE MATRIX BX AND VECTOR WX  OUT OF B (AND C)
      DO 8  J=1,JMAX
      JC=1+(J-1)*JMAX
    8 CALL EQUAL (BX(1,J),B(JC)  ,JMAX)
      CALL EQUAL (WX,B (JMAX*JMAX+1)  ,JMAX)
      CALL MVV (W,BX,A,JMAX,JMAX-1,NPDIM)
      CALL VADD (WX,W,JMAX)
C***  WX(J) IS THE FEAUTRIER-INTENSITY U AT RADIUS R(L)
      DO 3 J=1,JMAX
    3 U(L,J)=WX(J)
      CALL MOMENT0 (ND,RADIUS,L,JMAX,Z,WX,XJC(L),.FALSE.)
      CALL MOMENT2 (RL,JMAX,P,WX,XK)
      IF (XJC(L) .GT. .0) THEN
         EDDI(1,L)=XK/XJC(L)
      ELSE
         EDDI(1,L)=1./3.
         IWARN = IWARN + 1
      ENDIF

      IF (EDDI(1,L) .LT. 0.01) THEN
        IWARN = IWARN + 1
        EDDI(1,L) = 0.01
      ENDIF

C***  THIS IS AN INGENIOUS (;) RECURSION FORMULA FOR THE SPHERICITY FACTOR ]
      RLP=RADIUS(L+1)
      FLP=FL
      FL=3.-1./EDDI(1,L)
      RRQ=RRQ *EXP(FL-FLP)*(RL/RLP)**((FLP*RL-FL*RLP)/(RL-RLP))
      EDDI(2,L)=RRQ/RL/RL
    4 CALL EQUAL (A,WX,JMAX)

      CALL MOMENT1 (RADIUS(1), NP, P, WX, H)

C***  CORRECT THE EDDINGTON FLUX AT THE OUTER BOUDARY
C***    FOR A (POSSIBLE) INCIDENT RADIATION:
      H = H - 0.5 * XIMINUS

C***  EMFLUX IS THE EMERGENT FLUX, BUT RELATED TO THE INNER RADIUS
      EMFLUX=4.*H*RADIUS(1)*RADIUS(1)

C***  BOUNDARY EDDINGTON FACTOR
      IF (XJC(1) .GT. .0) THEN
         EDDI(3,1)=H/XJC(1)
      ELSE
         EDDI(3,1)=0.5
         IWARN = IWARN + 1
      ENDIF
 
      RETURN
      END
