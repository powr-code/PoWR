      SUBROUTINE BACKJCU (LBREF,INDREF,U,ND,NP,NPDIM,XLAMREF,Z,A,B,C,W,
     >              BX,WX,XJCIND,RADIUS,P,BCORE,DBDR,OPA,ETA,THOMSON,
     >              EDDI,ENTOT,NOLAP,NEWBGC,XLAMBDA,NF,XJC, IWARNJ0, 
     >              BFORMAL, FNUEC, BNOCONT, ST, BELIFI)
C**********************************************************************
C***  CALLED FROM: CMF
C***  - YIELDS THE BACKGROUND RADIATION FIELD FOR THE CURRENT LINE.
C***  THE ANGLE-DEPENDEND RADIATION FIELD, U, IS NEEDED FOR THE BLUE-WING
C***    BOUNDARY CONDITION OF THE LINES. IT IS EITHER READ FROM FILE OR, 
C***    IF IT IS NOT THERE, CALCULATED NEW BY SUBR. ELIMIN.
C***  U IS NOT NEEDED IF THE CONSIDERED LINE IS TREATED AS A BLEND,
C***    SO THAT ITS BLUE BOUNDARY COINCIDES WITH THE RED BOUNDARY OF THE
C***    PRECEEDING LINE. IN THIS CASE ONLY THE CONTINUUM INTENSITY AT 
C***    THE LINE FREQUENCY, XJCIND, IS NEEDED AND OBTAINED BY INTER-
C***    POLATION IN THE BACKGROUND CONTINUUM FIELD.
C***    >>>  NOTE: U IS SCALED TO THE INTENSITY XJC !  <<<<<<<
C**********************************************************************

      DIMENSION U(ND,NP), XJC(ND,NF), XLAMBDA(NF), XJCIND(ND)
      DIMENSION RADIUS(ND)
      LOGICAL NOLAP, BFORMAL, BNOCONT, BTRY

C *** NOTE: ASSURE 64-BIT TYPE FOR USE IN WRITMS
      CHARACTER*8 NAMEU, NAMEJ

      BTRY = .FALSE.

      IF (.NOT. BFORMAL) THEN
        WRITE (NAMEU,'(A3,I4)') 'U  ',INDREF
        WRITE (NAMEJ,'(A3,I4)') 'XJC',INDREF
      ENDIF

C***  IF THE ANGLE-DEPENDEND RADIATION FIELD, U, IS NEEDED: ***********
      IF ((NOLAP .OR.  LBREF .EQ. 1) .AND. .NOT. BFORMAL) THEN
C***       ... TRY TO READ U FROM FILE "BACKJCU"
           IERR=1
           CALL READMS (9,U,ND*NP,NAMEU,IERR)
           IF (IERR .EQ. -10) THEN
             BTRY = .TRUE.
           ENDIF
           IF (IERR .LT. 0 .AND. IERR. NE. -10) THEN
              CALL REMARK 
     >          ('SUBR. BACKJCU: ERROR WHEN READING U FROM FILE 9')
              STOP 'ERROR'
           ENDIF
      ENDIF

C***     IF DATA FOR THAT LINE NOT PRESENT: CALCULATE IT NEW
         IF (BTRY .OR. BFORMAL) THEN
           CALL ELIMIN (XLAMREF,FNUEC,DUMMY2,U,Z,A,B,C,W,BX,WX,XJCIND,
     $         RADIUS,P,BCORE,DBDR,OPA,ETA,THOMSON,EDDI,ND,NP,NPDIM,
     $         ENTOT,INDREF, IDUMMY, ST, BELIFI, 0)
         ENDIF
C      GOTO 999

C***  INTERPOLATION OF THE CONTINUUM RADIATION FIELD, STORED IN
C***    XJCIND. XJCIND FROM ELIMIN IS OVERWRITTEN
      IF (.NOT. BFORMAL .OR. (BFORMAL .AND. .NOT. BNOCONT)) THEN
        CALL BACKJC (XJC, ND, NF, XJCIND, XLAMBDA, XLAMREF, RADIUS)
      ENDIF

C***  IF U IS NEEDED:
      IF ( NOLAP .OR. LBREF .EQ. 1 .OR. BFORMAL) THEN

C***     IF U HAS BEEN CALCULATED NEW BY CALLING "ELIMIN", IT IS NOW
C***     SCALED TO THE (INTERPOLATED) XJC AND WRITTEN ON THE BACKJCU-FILE
         IF (BTRY .OR. BFORMAL) THEN
            DO 2 J=1, NP
            LMAX = MIN0 (NP+1-J, ND)
            DO 2 L=1, LMAX
            IF (XJCIND(L) .EQ. .0) THEN
               U(L,J) = 1.
               IWARNJ0 = IWARNJ0 + 1
               ELSE
               U(L,J) = U(L,J) / XJCIND(L)
            ENDIF
    2       CONTINUE
            NEWBGC = NEWBGC + 1
            IF (.NOT. BFORMAL) THEN
              CALL WRITMS (9, U     , ND*NP, NAMEU, -1, IDUMMY, IERR)

C***          NOTE: XJC IS WRITTEN ONLY FOR TEST PURPOSES, BUT NEVER NEEDED.
              CALL WRITMS (9, XJCIND, ND   , NAMEJ, -1, IDUMMY, IERR)
            ENDIF

         ENDIF

C***     DE-SCALE U IT WITH THE PRESENT XJC
         DO 3 J=1, NP
         LMAX = MIN0 (NP+1-J, ND)
         DO 3 L=1, LMAX
    3    U(L,J) = U(L,J) * XJCIND(L)

      ENDIF

      RETURN
      END
