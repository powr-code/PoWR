      SUBROUTINE OVERLAP (IBLENDS,MAXLAP,LASTIND,XLAMZERO,XLAMRED,
     >           XLAMBLUE,XMAX,EINST,NDIM,ELEVEL,INDNUP,INDLOW,NOLAP,
     >           VDOP,NBLENDS,LASTFE)

C**********************************************************************
C***  CALLED FROM: STEAL
C***  FILLS THE MATRIX "IBLENDS": ROW 'IND' CONTAINS THE INDICES
C***     OF ALL LOCALLY OVERLAPPING LINES
C**********************************************************************

      DIMENSION IBLENDS(MAXLAP,LASTIND),INDNUP(LASTIND),INDLOW(LASTIND)
      DIMENSION XLAMZERO(LASTIND), XLAMBLUE(LASTIND), XLAMRED(LASTIND)
      DIMENSION EINST(NDIM,NDIM), ELEVEL(NDIM), NBLENDS(LASTIND)
      LOGICAL NOLAP

C***  VELOCITY OF LIGHT (IN KM/SEC)
      DATA CLIGHT / 2.99792458E5 /

C***  INITIALIZATION AND WAVELENGHTS COMPUTATION
      DO 100 IND=1,LASTIND
      XLAMZERO(IND) = 1.E8 / (ELEVEL(INDNUP(IND))-ELEVEL(INDLOW(IND)))
      XLAMBLUE(IND) = XLAMZERO(IND) * (1. - XMAX * VDOP / CLIGHT)
      XLAMRED(IND)  = XLAMZERO(IND) * (1. + XMAX * VDOP / CLIGHT)
        DO 150 LB=1, MAXLAP
  150   IBLENDS(LB,IND) = 0
  100 CONTINUE

C***  LOOP OVER ALL LINES (EXCEPT IRON-LINES)
      DO 200 IND =1, (LASTIND-LASTFE)
C***  RUDIMENTAL LINE?
       IF (EINST(INDLOW(IND),INDNUP(IND)) .EQ. -2.) GOTO 200

C***  SPECIAL BRANCH IF OVERLAP OPTION IS NOT ACTIVE
      IF (NOLAP) THEN
         IBLENDS(1,IND) = IND
         NBLENDS(IND)=1
         ELSE

         LB = 0
         DO 300 INDTEST=1, (LASTIND-LASTFE)
         LOW = INDLOW(INDTEST)
         NUP = INDNUP(INDTEST)
C***  TEST LINE NOT RUDIMENTAL?
         IF (EINST(LOW,NUP) .NE. -2.) THEN

C***  TEST LINE OVERLAPPING?
            IF ( (  XLAMBLUE(IND) .LT. XLAMRED (INDTEST)  .AND.
     >            XLAMZERO(IND) .GE. XLAMZERO(INDTEST) )
     >           .OR.
     >           ( XLAMRED (IND) .GT. XLAMBLUE(INDTEST)   .AND.
     >            XLAMZERO(IND) .LE. XLAMZERO(INDTEST) )
     >           ) THEN
               LB = LB + 1
C***           DIMENSION CHECK
               IF (LB .GT. MAXLAP) THEN
                  ASSIGN 1000 TO LABEL
1000              FORMAT ('OVERLAP: DIMENSION MAXLAP = ', I4,
     >                    ' .LT. LB=', I4)
                  CALL REMARKF(LABEL, MAXLAP, LB)
                  STOP 'ERROR'
                  ENDIF
               IBLENDS(LB,IND) = INDTEST
               ENDIF
            ENDIF
  300    CONTINUE
         NBLENDS(IND)=LB
         ENDIF
  200 CONTINUE

      RETURN
      END
